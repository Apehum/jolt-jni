// Gradle script to build/publish jolt-jni Android artifacts

plugins {
    alias(libs.plugins.android.library) // to build Android libraries
}

// $artifact is set in the gradle.properties file
// or by a -Partifact= option on the command line.

android {
    buildFeatures {
        viewBinding = true
    }
    buildTypes {
        debug {
            externalNativeBuild {
                ndkBuild {
                    cppFlags '-DJPH_DEBUG_RENDERER'
                    cppFlags '-DJPH_ENABLE_ASSERTS'
                }
            }
            jniDebuggable = true
        }
        release {
            externalNativeBuild {
                ndkBuild {
                    cppFlags '-DJPH_NO_DEBUG'
                }
            }
            minifyEnabled = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    compileSdk = 35
    defaultConfig {
        aarMetadata {
            minCompileSdk = 33
        }
        minSdk = 33
        ndk {
            abiFilters 'arm64-v8a'
        }
        targetSdk = 33
    }
    externalNativeBuild {
        ndkBuild {
            path 'Android.mk'
        }
    }
    flavorDimensions 'precision'
    namespace = 'com.github.stephengold.joltjni'
    ndkVersion = '27.2.12479018' // r27c
    productFlavors {
        Dp { // flavor with double-precision locations
            dimension 'precision'
            externalNativeBuild {
                ndkBuild {
                    cppFlags '-DJPH_DOUBLE_PRECISION'
                }
            }
        }
        Sp { // flavor with single-precision locations
            dimension 'precision'
        }
    }
    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }
}

dependencies {
    testImplementation(libs.jsnaploader)
    testImplementation(libs.junit4)
}
